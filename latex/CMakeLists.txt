set(TARGET_NAME argon2-specs)
PROJECT(${TARGET_NAME} NONE)
find_package(LATEX COMPONENTS PDFLATEX BIBTEX)
message(STATUS "PDFLATEX_COMPILER: ${PDFLATEX_COMPILER}")
message(STATUS "BIBTEX_COMPILER: ${BIBTEX_COMPILER}")

set(latex_log "${CMAKE_CURRENT_BINARY_DIR}/build_${TARGET_NAME}.log")

add_custom_command(OUTPUT "${TARGET_NAME}.aux"
    COMMAND ${PDFLATEX_COMPILER} -output-directory=${CMAKE_CURRENT_BINARY_DIR} "${CMAKE_CURRENT_SOURCE_DIR}/${TARGET_NAME}.tex" > ${latex_log} 2>&1
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    DEPENDS
    "${CMAKE_CURRENT_SOURCE_DIR}/${TARGET_NAME}.tex"
    "${CMAKE_CURRENT_SOURCE_DIR}/IEEEtran.cls")

add_custom_command(OUTPUT "${TARGET_NAME}.bbl" "${TARGET_NAME}.blg"
    COMMAND ${CMAKE_COMMAND} -E copy
        ${CMAKE_CURRENT_SOURCE_DIR}/tradeoff.bib
        ${CMAKE_CURRENT_BINARY_DIR}/tradeoff.bib
    COMMAND ${BIBTEX_COMPILER} "${TARGET_NAME}.aux" >> ${latex_log} 2>&1
    MAIN_DEPENDENCY
    "${CMAKE_CURRENT_SOURCE_DIR}/tradeoff.bib"
    DEPENDS
    "${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}.aux"
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})

add_custom_target("${TARGET_NAME}" ALL
    COMMAND ${PDFLATEX_COMPILER} -output-directory=${CMAKE_CURRENT_BINARY_DIR} "${CMAKE_CURRENT_SOURCE_DIR}/${TARGET_NAME}.tex" >> ${latex_log} 2>&1
    COMMAND ${PDFLATEX_COMPILER} -output-directory=${CMAKE_CURRENT_BINARY_DIR} "${CMAKE_CURRENT_SOURCE_DIR}/${TARGET_NAME}.tex" >> ${latex_log} 2>&1
    BYPRODUCTS "${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}.pdf"
    DEPENDS
    "${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}.bbl"
    "${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}.blg"
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})
